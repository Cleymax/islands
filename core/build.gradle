project.version = '0.2.9'

dependencies {
    runtime project(":api")
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.+')
    }
}

apply plugin: "com.jfrog.artifactory"

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name, 'Implementation-Version': version, 'Main-Class': 'fr.islandswars.core.IslandsCore'
    }
}

task fatJar(type: Jar) {
    archiveName = projectName + '.jar'
    from { (configurations.runtime - configurations.provided).collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

apply plugin: 'maven-publish'

artifactory {
    def branch = "$System.env.TRAVIS_BRANCH"
    println 'Build on branch : ' + branch
    if (branch != 'master')
        version += '-' + branch

    publish {
        contextUrl = 'http://repo.islandswars.fr:8081/artifactory'
        repository {
            repoKey = 'islandswars'
            username = System.env.ARTIFACTORY_USER
            password = System.env.ARTIFACTORY_PASSWORD
        }
        defaults {
            publications('mavenJava')
        }
    }

}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar

            artifact javadocJar

            if (project.tasks.findByName('fatJar')) {
                artifact fatJar
            }
        }
    }
}